PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  <https://id.milieuinfo.be/dataset/imjv#id>
    <http://rdfs.org/ns/void#classPartition> [
      <http://rdfs.org/ns/void#class> ?type;
      <http://rdfs.org/ns/void#triples> ?totalNumberOfTriples;
      <http://rdfs.org/ns/void#entities> ?totalNumberOfEntities;
      <http://rdfs.org/ns/void#classes> ?totalNumberOfClasses;
      <http://rdfs.org/ns/void#properties> ?totalNumberOfProperties;
      <http://rdfs.org/ns/void#distinctSubjects> ?totalNumberOfDistinctSubjects;
      <http://rdfs.org/ns/void#distinctObjects> ?totalNumberOfDistinctObjects;

    ]
    .
}
WHERE
{

  SELECT DISTINCT

    ?type
    ?totalNumberOfTriples
    ?totalNumberOfEntities
    ?totalNumberOfClasses
    ?totalNumberOfProperties
    ?totalNumberOfDistinctSubjects
    ?totalNumberOfDistinctObjects

  {

    # total number of triples
    {
      SELECT ?totalNumberOfTriples ?type {
        SELECT (COUNT(*) AS ?totalNumberOfTriples) ?type {
          GRAPH ?g {
            ?s ?p ?o; a ?type
          }
        }
        GROUP BY ?type
      }
    }

    # total number of entities
    {
      SELECT ?totalNumberOfEntities ?type {
        SELECT (COUNT(distinct ?s) AS ?totalNumberOfEntities) ?type {
          GRAPH ?g {
            ?s a []; a ?type
          }
        }
        GROUP BY ?type
      }
    }

    # total number of classes
    {
      SELECT ?totalNumberOfClasses ?type {
        SELECT (COUNT(distinct ?type) AS ?totalNumberOfClasses) {
          GRAPH ?g {
            ?s a ?type
          }
        }
        GROUP BY ?type
      }
    }

    # total number of properties
    {
      SELECT ?totalNumberOfProperties ?type {
        SELECT (COUNT(distinct ?p) AS ?totalNumberOfProperties) ?type {
          GRAPH ?g {
            ?s ?p ?o; a ?type
          }
        }
        GROUP BY ?type
      }
    }


    # total number of distinct subjects
    {
      SELECT ?totalNumberOfDistinctSubjects ?type {
        SELECT (COUNT(DISTINCT ?s) AS ?totalNumberOfDistinctSubjects) ?type {
          GRAPH ?g {
            ?s ?p ?o; a ?type
          }
        }
        GROUP BY ?type
      }
    }

    # total number of distinct objects
    {
      SELECT ?totalNumberOfDistinctObjects ?type {
        SELECT (COUNT(DISTINCT ?o) AS ?totalNumberOfDistinctObjects) ?type {
          GRAPH ?g { # <https://id.milieuinfo.be/graph/conceptscheme/BO#id>
            ?s ?p ?o; a ?type . FILTER (!isLiteral(?o))
          }
        }
        GROUP BY ?type
      }
    }

  }

}

